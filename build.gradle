
apply plugin: 'java'
apply plugin: 'eclipse'

pluginVersion = '0.0.3'
esVersion = '0.18.6'
luceneVersion = '3.5.0'
archivesBaseName = "elasticsearch-suggest-" + pluginVersion

explodedDistDir = new File(distsDir, 'exploded')

// TODO: Possibly make implementation date dynamic?
manifest.mainAttributes("Implementation-Title": "ElasticSearch::Plugins::Suggest", "Implementation-Version": rootProject.version, "Implementation-Date": "2011")

// no need to use the resource dir
sourceSets.main.resources.srcDirs 'src/main/java'
sourceSets.test.resources.srcDirs 'src/test/java'
sourceSets.test.resources.srcDirs 'src/test/resources'

configurations {
    dists
    distLib {
        visible = false
        transitive = false
    }
}

repositories {
    mavenCentral()
    mavenRepo urls: "http://oss.sonatype.org/content/repositories/releases/"
}

dependencies {
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: esVersion, transitive: true
    compile "org.apache.lucene:lucene-misc:" + luceneVersion
    compile "org.apache.lucene:lucene-spellchecker:" + luceneVersion
    distLib "org.apache.lucene:lucene-misc:" + luceneVersion
    distLib "org.apache.lucene:lucene-spellchecker:" + luceneVersion
    testCompile "org.hamcrest:hamcrest-core:1.2"
    testCompile "org.hamcrest:hamcrest-library:1.2"
    testCompile "junit:junit-dep:4.8.2"
    testCompile "commons-io:commons-io:2.0.1"
    testCompile "com.ning:async-http-client:1.6.2"
    testCompile "org.slf4j:slf4j-log4j12:1.6.1"
}

task explodedDist(dependsOn: [jar], description: 'Builds the plugin zip file') << {
    [explodedDistDir]*.mkdirs()

    copy {
        from configurations.distLib
        into explodedDistDir
    }

    // remove elasticsearch files (compile above adds the elasticsearch one)
    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*.jar") }

    copy {
        from libsDir
        into explodedDistDir
    }

    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*-javadoc.jar") }
    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*-sources.jar") }
}

task zip(type: Zip, dependsOn: ['explodedDist']) {
    from(explodedDistDir) {
    }
}

task release(dependsOn: [zip]) << {
    ant.delete(dir: explodedDistDir)
    copy {
        from distsDir
        into(new File(rootProject.distsDir, "plugins/suggest"))
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-http:1.0-beta-2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

eclipseClasspath {
    defaultOutputDir = file('build/eclipse-build')
}
